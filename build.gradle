import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'base'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.5'
    id 'de.undercouch.download' version '2.1.0'
}

group = 'io.github.ssoloff'

ext {
    downloadsDir = file("$buildDir/tmp/downloads")
    jeditPluginBinDistTar = file("$downloadsDir/$packageName-${version}-bin.tar.gz")
    jeditPluginBinDistZip = file("$downloadsDir/$packageName-${version}-bin.zip")
    jeditPluginJar = file("$libsDir/$packageName-${version}.jar")
}

task downloadJEditPluginBinDistTar(type: Download) {
    src "http://downloads.sourceforge.net/project/jedit-plugins/$pluginName/$version/$pluginName-$version-bin.tgz"
    dest jeditPluginBinDistTar
    onlyIfNewer true
}

task downloadJEditPluginBinDistZip(type: Download) {
    src "http://downloads.sourceforge.net/project/jedit-plugins/$pluginName/$version/$pluginName-$version-bin.zip"
    dest jeditPluginBinDistZip
    onlyIfNewer true
}

task prepareJEditPluginJar(dependsOn: downloadJEditPluginBinDistTar) {
    outputs.file jeditPluginJar

    doLast {
        copy {
            from tarTree(downloadJEditPluginBinDistTar.dest).files
            into libsDir
            include "${pluginName}.jar"
            rename { jeditPluginJar.name }
        }
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = '2.10'
}

bintrayUpload.dependsOn(
    downloadJEditPluginBinDistTar,
    downloadJEditPluginBinDistZip,
    prepareJEditPluginJar
)

publishing {
    publications {
        maven(MavenPublication) {
            artifact jeditPluginJar
            artifactId packageName

            artifact source: jeditPluginBinDistTar, classifier: 'bin', extension: 'tar.gz'
            artifact source: jeditPluginBinDistZip, classifier: 'bin', extension: 'zip'
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    dryRun = project.properties['bintrayDryRun'] ?: false
    publish = true
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = "io.github.ssoloff:$packageName"
        desc = "jEdit $pluginName plugin"
        websiteUrl = 'https://github.com/ssoloff/jedit-plugins'
        issueTrackerUrl = 'https://github.com/ssoloff/jedit-plugins/issues'
        vcsUrl = 'https://github.com/ssoloff/jedit-plugins.git'
        licenses = ['GPL-2.0']
        publicDownloadNumbers = true

        version {
            name = "${project.version}"
            desc = "jEdit $pluginName plugin ${project.version}"
        }
    }
}
