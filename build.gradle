plugins {
    id 'base'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.5'
    id 'de.undercouch.download' version '2.1.0'
}

group = 'io.github.ssoloff'

ext {
    downloadsDir = file("$buildDir/tmp/downloads")
    jeditPluginBinDistTarUrl = "http://downloads.sourceforge.net/project/jedit-plugins/$pluginName/$version/$pluginName-$version-bin.tgz"
    jeditPluginBinDistTar = file("$downloadsDir/$packageName-${version}-bin.tar.gz")
    jeditPluginBinDistZipUrl = "http://downloads.sourceforge.net/project/jedit-plugins/$pluginName/$version/$pluginName-$version-bin.zip"
    jeditPluginBinDistZip = file("$downloadsDir/$packageName-${version}-bin.zip")
    jeditPluginJar = file("$libsDir/$packageName-${version}.jar")
}

task downloadJEditPlugin {
    outputs.file jeditPluginJar

    doLast {
        download {
            src jeditPluginBinDistTarUrl
            dest jeditPluginBinDistTar
        }

        download {
            src jeditPluginBinDistZipUrl
            dest jeditPluginBinDistZip
        }

        copy {
            from tarTree(jeditPluginBinDistTar).files
            into libsDir
            include "${pluginName}.jar"
            rename { jeditPluginJar.name }
        }
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = '2.10'
}

bintrayUpload.dependsOn downloadJEditPlugin

publishing {
    publications {
        maven(MavenPublication) {
            artifact jeditPluginJar
            artifactId packageName

            artifact source: jeditPluginBinDistTar, classifier: 'bin', extension: 'tar.gz'
            artifact source: jeditPluginBinDistZip, classifier: 'bin', extension: 'zip'
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publish = true
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = "io.github.ssoloff:$packageName"
        desc = "jEdit $pluginName plugin"
        websiteUrl = 'https://github.com/ssoloff/jedit-plugins'
        issueTrackerUrl = 'https://github.com/ssoloff/jedit-plugins/issues'
        vcsUrl = 'https://github.com/ssoloff/jedit-plugins.git'
        licenses = ['GPL-2.0']
        publicDownloadNumbers = true

        version {
            name = "${project.version}"
            desc = "jEdit $pluginName plugin ${project.version}"
        }
    }
}
